version = '0.0.1-SNAPSHOT'

dependencies {
    implementation project(path:':common-cvi', configuration: 'default')
    implementation project(path:':domain-cvi', configuration: 'default')
    implementation project(path:':domain-cvi-oauth-service', configuration: 'default')
    implementation project(path:':domain-cvi-publicdata-service', configuration: 'default')
    implementation project(path:':domain-cvi-scheduler', configuration: 'default')
    implementation project(path:':domain-cvi-aws-s3-service', configuration: 'default')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    //cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'org.ehcache:ehcache:3.9.4'

    testImplementation 'io.rest-assured:rest-assured:4.3.3'
}

jacocoTestReport {
    dependsOn test // 리포트가 만들어지기 전 테스트 실행되어야 한다.
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, // 리포트 결과에 제외시킬 디렉토리
                    exclude: ['**/DataLoader*',
                              '**/CentralVaccinationInformationApplication*',
                              '**/DummyData*'])
        }))
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification { // 코드 커버리지 측정항목 시행
    violationRules { // 위반 규칙
        rule {
            enabled = true
            element = 'GROUP'

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = [
                    // 커버리지 제외할 클래스
                    '*.DataLoader*',
                    '*.CentralVaccinationInformationApplication*',
                    '*.DummyData*',
            ]
        }
    }
}

sonarqube {
    properties {
        property "sonar.exclusions", "**/*Test*.*, **/Q*.java, **/*Doc*.java, **/CentralVaccinationInformationApplication.java, **/DataLoader.java, **/DummyData.java"
    }
}
